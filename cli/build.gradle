plugins {
    id 'application'
    id 'com.gradleup.shadow' version '9.2.0'
    id 'maven-publish'
}

application {
    mainClass = 'io.aether.cli.CLI'
    applicationDefaultJvmArgs = [
            '--add-opens=java.base/jdk.internal.misc=ALL-UNNAMED',
            '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED'
    ]
}

shadowJar {
    archiveFileName.set('aether-cli.jar')
    archiveVersion.set(version.toString())
    manifest {
        attributes 'Main-Class': 'io.aether.cli.CLI'
    }
}

dependencies {
    implementation project(':client')
    implementation("org.slf4j:slf4j-nop:2.0.16")
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.aether.crypto:hydrogen-fast:+'
    implementation 'io.aether.crypto:sodium:+'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

tasks.register('dist') {
    dependsOn('shadowJar')
    doLast {
        var jar = new FileInputStream("$project.buildDir/libs/aether-cli.jar");
        var outputDir = new File("$project.buildDir/bin")
        if (!outputDir.exists()) {
            outputDir.mkdirs()
        }
        var outputFile = new File(outputDir, "aether-cli")
        var out = new FileOutputStream(outputFile)
        // Добавляем JVM аргументы в shell-скрипт
        out.write("#!/bin/sh\nexec java --add-opens=java.base/jdk.internal.misc=ALL-UNNAMED --add-opens=java.base/sun.nio.ch=ALL-UNNAMED -jar \$0 \"\$@\"\nexit 0\n".getBytes())
        jar.transferTo(out)
        out.close()
        jar.close()
        outputFile.setExecutable(true)
    }
}

tasks.register('updateVersion') {
    doLast {
        def fw = new FileWriter(new File(System.getProperty("java.io.tmpdir"), "aether-cli-version.txt"));
        fw.write(version.toString());
        fw.close();
    }
}

processResources {
    dependsOn 'updateVersion'
    from(System.getProperty("java.io.tmpdir") + "/aether-cli-version.txt") {
        into(".")
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId "io.aether"
            artifactId "cli"
        }
    }
}