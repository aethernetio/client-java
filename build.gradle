if (gradle.parent != null) {
    tasks.register("updateVersion") {
        doLast {
            var fVersion = new File(projectDir, "version");
            var v = fVersion.getText('UTF-8').toInteger();
            v++;
            fVersion.write(v.toString());
            rootProject.version '0.0.' + v
            subprojects {
                version rootProject.version
            }
        }
    }
    tasks.register("aetherPub") {
        dependsOn("updateVersion")
        dependsOn(":client:publish")
        dependsOn(":pointToPoint:publish")
        dependsOn(":cli:publish")
        dependsOn(":chat:publish")
    }
}

group 'io.aether'
version '0.0.' + (new File(projectDir, "version").getText('UTF-8'))
subprojects {
    group rootProject.group
    version rootProject.version
    repositories {
        mavenCentral()
        maven {
            url 'http://nexus.aethernet.io/repository/maven-releases/'
            allowInsecureProtocol = true
        }
    }
    afterEvaluate {
        if (gradle.parent != null) {
            if (it.plugins.hasPlugin('maven-publish')) {
                publishing {
                    repositories {
                        maven {
                            def releasesUrl = uri("http://nexus.aethernet.io/repository/maven-releases/")
                            def snapshotsUrl = uri("http://nexus.aethernet.io/repository/maven-snapshots/")
                            if (version.toString().endsWith("SNAPSHOT")) {
                                url(snapshotsUrl)
                            } else {
                                url(releasesUrl)
                            }
                            allowInsecureProtocol = true

                            credentials {
                                username gradle.parent.rootProject.properties["aetherRepoUser"] as String
                                password gradle.parent.rootProject.properties["aetherRepoPassword"] as String
                            }
                        }
                    }
                }
            }
        }
        if (it.plugins.hasPlugin('java')) {
            java {
                toolchain {
                    languageVersion = JavaLanguageVersion.of(11)
                }
                withJavadocJar()
                withSourcesJar()
            }
            def isLib = it.plugins.hasPlugin('java-library')
            compileJava.options.encoding = "UTF-8"
            compileTestJava.options.encoding = "UTF-8"
            dependencies {
                if (isLib) {
                    api 'org.jetbrains:annotations:24.0.1'
                    api 'it.unimi.dsi:fastutil:8.5.12'
                } else {
                    implementation 'org.jetbrains:annotations:24.0.1'
                    implementation 'it.unimi.dsi:fastutil:8.5.12'
                }
                testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
                testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
            }

            javadoc.options.addStringOption('Xdoclint:none', '-quiet')
            test {
                useJUnitPlatform()
                testLogging.showStandardStreams = true
                enableAssertions = true
            }
            compileTestJava {
                options.compilerArgs += ['-parameters']
            }
            compileJava {
                options.compilerArgs += ['-parameters',
                                         '-Xlint:unchecked',
                                         '-Xlint:deprecation'
                ]
            }
        }
    }

}